// Parser for TI58/TI59 programming language for Lark
// Compatible with both Earley and LALR(1) parsers
//
// Note: to use INV alone or in a non-standard context, use INV! instead
// This enables a clean integration of INV with instructions supporting it
// Extensions: 
// - Multiple (case-insensitive) mnemonic names: 'x²' 'x^2' and 'X2' are identical
// - Comments: # until end of line
// - Extra instructions such as e^x for INV lnx...
// - Numbers accepted in usual form: 6.02e23, -56.8612, ...
// - Two digits are accepted as equivalents of key names for labels: Lbl 25 is equivalent to Lbl CLR
// Some weird mnemonic variants support format of .t59 files such as YX for y^x,
// addresses such as 02 73, X2 for x², IXI for |x|, STA for Σ+, AVR for x̄...
// Some mnemonics are not valid instructions (SST, LRN, Ins...) but are accepted
// as labels (Lbl SST, Lbl LRN, Lbl Ins...)
//
// 2025-11-08   PV      First version

NUMBER: /-?\d+(\.\d+)?([eE][+-]?\d+)?(?=$|[ \t\n\r])/
WS: /[ \t\n\r]+/

start: WS* | WS* instruction_or_comment (WS instruction_or_comment)* WS*

instruction_or_comment: instruction
		| comment

comment: /#.*/

instruction: NUMBER
		| i93_dot
		| i94_change_sign
		| exponent
		| i53_left_parenthesis
		| i54_right_parenthesis
		| operator
		| i95_equals
		| invert "!"        // Trick to allow a standalone Inv while keeping grammar simple
		| fix
		| atomic_instruction
		| clear_instruction
		| memory_or_flag_instruction
		| label_instruction
		| branch_instruction
		| conditional_instruction
		| op_instruction
		| pgm_instruction

single_digit:   /\d/
memory:         /\d\d?/                         // Register number
pgm_number:     /\d\d?/
address_label:  /(\d\d\d)|(0\d[ \r\n]+\d\d)/    // The version nn<space>nn is for T59 programs
numeric_key_label: /[1-9][0-9]/                 // 10..99
flag_number:    /0?[0-7]/
op_number:      /([0-3]?[0-9])|40/              // TI-58C has Op 40 (printer detection)

i11_a: "A"i
i12_b: "B"i
i13_c: "C"i
i14_d: "D"i
i15_e: "E"i
i16_a_prime: "A'"i
i17_b_prime: "B'"i
i18_c_prime: "C'"i
i19_d_prime: "D'"i
i10_e_prime: "E'"i

i21_2nd: "2nd"i
i22_invert: "Inv"i
i23_ln: "lnx"i
i24_correct_entry: "CE"i
i25_clear: "CLR"i
i26_2nd_2nd: "2nd'"i
i27_2nd_invert: "INV'"i
i28_log: "log"i
i29_clear_program: "CP"i
i20_2nd_clear: "CLR'"i

i31_learn: "LRN"i
i32_exchange_x_and_t: "x<>t"i | "x/t"i
i33_square: "x²"i | "x^2"i | "X2"i
i34_square_root: "√" | "√x"i | "SQR"i | "SQRT"i
i35_reciprocal: "1/x"i
i36_program: "Pgm"i
i37_polar_to_rectangular: "P->R"i | "P/R"i
i38_sin: "sin"i
i39_cos: "cos"i
i30_tan: "tan"i

i41_single_step: "SST"i
i42_store: "STO"i
i43_recall: "RCL"i
i44_sum: "SUM"i
i45_power: "^" | "y^x"i | "YX"i | "**"
i46_insert: "Ins"i
i47_clear_memory: "CMs"i
i48_exchange: "Exc"i
i49_product: "Prd"i
i40_indirect: "Ind"i

i51_backstep: "BST"i
i52_exponent: "EE"i
i53_left_parenthesis: "("
i54_right_parenthesis: ")"
i55_divide: "/" | "÷"
i56_delete: "Del"i
i57_engineering: "Eng"i
i58_fix: "Fix"i
i59_integer: "Int"i
i50_absolute: "|x|"i | "ABS"i | "IXI"i

i61_goto: "GTO"i
i62_program_indirect: "PG*"i
i63_exchange_indirect: "EX*"i
i64_product_indirect: "PD*"
i65_multiply: "*" | "×"
i66_pause: "Pause"i | "PAU"i
i67_x_equals_t: "x=t"i | "EQ"i
i68_nop: "Nop"i
i69_operation: "Op"i
i60_degrees: "Deg"i

i71_subroutine: "SBR"i
i72_store_indirect: "ST*"i
i73_recall_indirect: "RC*"i
i74_sum_indirect: "SM*"i
i75_subtract: "-"
i76_label: "Lbl"i
i77_x_greater_or_equal_than_t: "x>=t"i | "GE"i
i78_sigma_plus: "Σ+" | "SIG+"i | "STA"i
i79_average: "x̄"i | "AVG"i | "AVR"i
i70_radians: "Rad"i

i81_reset: "RST"i
i82_hir: "HIR"i
i83_goto_indirect: "GO*"i
i84_operation_indirect: "Op*"i
i85_add: "+"
i86_set_flag: "STF"i
i87_if_flag: "IFF"i
i88_dms: "DMS"i | "D.MS"i
i89_pi: "π" | "PI"i
i80_grades: "Grad"i | "GRD"i

i91_run_stop: "R/S"i
i92_return: "RTN"i
i93_dot: "."
i94_change_sign: "+/-"
i95_equals: "="
i96_write: "Write"i | "Wrt"i
i97_dsz: "Dsz"i
i98_advance: "Adv"i
i99_print: "Prt"i
i90_list: "List"i | "LST"i

exponent: [invert WS?] i52_exponent

operator: i85_add | i75_subtract | i65_multiply | i55_divide | i45_power

invert: i22_invert

fix:    i58_fix WS? single_digit_or_indirect

atomic_instruction: [invert WS?] i23_ln
		| "e^x"i
		| [invert WS?] i28_log
		| "10^x"i
		| i32_exchange_x_and_t
		| i33_square
		| i34_square_root
		| i35_reciprocal
		| [invert WS?] i37_polar_to_rectangular
		| [invert WS?] i38_sin
		| [invert WS?] i39_cos
		| [invert WS?] i30_tan
		| [invert WS?] i57_engineering
		| [invert WS?] i58_fix
		| [invert WS?] i59_integer
		| i50_absolute
		| i66_pause
		| i68_nop
		| i60_degrees
		| [invert WS?] i78_sigma_plus
		| [invert WS?] i79_average
		| i70_radians
		| [invert WS?] i88_dms
		| i89_pi
		| i80_grades
		| i91_run_stop
		| i96_write
		| i98_advance
		| i99_print
		| i90_list
		
clear_instruction: i25_clear | i24_correct_entry | i29_clear_program | i47_clear_memory

memory_or_flag_instruction: i42_store WS? memory_or_indirect
		| i72_store_indirect WS? memory
		| i43_recall WS? memory_or_indirect
		| i73_recall_indirect WS? memory
		| i48_exchange WS? memory_or_indirect
		| i63_exchange_indirect WS? memory
		| [invert WS?] i44_sum WS? memory_or_indirect
		| [invert WS?] i74_sum_indirect WS? memory
		| [invert WS?] i49_product WS? memory_or_indirect
		| [invert WS?] i64_product_indirect WS? memory
		| [invert WS?] i86_set_flag WS? flag_or_indirect
		| i82_hir WS? memory
		
memory_or_indirect: memory | indirect_memory

indirect_memory: i40_indirect WS? memory

label_instruction: i76_label WS? ( key_label | numeric_key_label ) /[ \t\n\r]:/?

branch_instruction: i61_goto WS? address_or_label_or_indirect
		| i83_goto_indirect WS? memory
		| i71_subroutine WS? address_or_label_or_indirect
		| i92_return WS? | invert WS? i71_subroutine
		| i81_reset
		| i11_a | i12_b | i13_c | i14_d | i15_e
		| i16_a_prime | i17_b_prime | i18_c_prime | i19_d_prime | i10_e_prime

address_or_label_or_indirect: address_label
		| key_label
		| numeric_key_label
		| indirect_memory

key_label: | i11_a | i12_b | i13_c | i14_d | i15_e
		| i16_a_prime | i17_b_prime | i18_c_prime | i19_d_prime | i10_e_prime

		| i21_2nd | i22_invert | i23_ln | i24_correct_entry | i25_clear
		| i26_2nd_2nd | i27_2nd_invert | i28_log | i29_clear_program | i20_2nd_clear
		
		| i31_learn | i32_exchange_x_and_t | i33_square | i34_square_root | i35_reciprocal
		| i36_program | i37_polar_to_rectangular | i38_sin | i39_cos | i30_tan

		| i41_single_step | i42_store | i43_recall | i44_sum | i45_power | 
		| i46_insert | i47_clear_memory | i48_exchange | i49_product | i40_indirect

		| i51_backstep | i52_exponent | i53_left_parenthesis | i54_right_parenthesis | i55_divide
		| i56_delete | i57_engineering | i48_exchange | i59_integer | i50_absolute

		| i61_goto | i62_program_indirect | i63_exchange_indirect | i64_product_indirect | i65_multiply
		| i66_pause | i67_x_equals_t | i68_nop | i69_operation | i60_degrees

		| i71_subroutine | i72_store_indirect | i73_recall_indirect | i74_sum_indirect | i75_subtract
		| i76_label | i77_x_greater_or_equal_than_t | i78_sigma_plus | i79_average | i70_radians

		| i81_reset | i82_hir | i83_goto_indirect | i84_operation_indirect | i85_add
		| i86_set_flag | i87_if_flag | i88_dms | i89_pi | i80_grades

		| i91_run_stop | i92_return | i93_dot | i94_change_sign | i95_equals
		| i96_write | i97_dsz | i98_advance | i99_print | i90_list

flag_or_indirect: flag_number | indirect_memory

conditional_instruction: x_equals_t_statement
		| x_greater_or_equal_than_t_statement
		| decrement_and_skip_on_zero_statement
		| test_flag_statement

// x=t
x_equals_t_statement: [invert WS?] i67_x_equals_t WS? address_or_label_or_indirect

// x>=t
x_greater_or_equal_than_t_statement: [invert WS?] i77_x_greater_or_equal_than_t WS? address_or_label_or_indirect

// Dsz (note that while officially only memories from 0 to 9 are supported, in fact all 99 memories are supported)
decrement_and_skip_on_zero_statement: [invert WS?] i97_dsz WS? single_digit_or_indirect WS address_or_label_or_indirect

single_digit_or_indirect: "0"? single_digit | indirect_memory

// If flg
test_flag_statement: [invert WS?] i87_if_flag WS? flag_or_indirect WS? address_or_label_or_indirect

// Op
op_instruction: i69_operation WS? op_number_or_indirect
		| i84_operation_indirect WS? memory

op_number_or_indirect: op_number | indirect_memory 

// Pgm
pgm_instruction: i36_program WS? pgm_number_or_indirect
		| i62_program_indirect WS? pgm_number

pgm_number_or_indirect: pgm_number | indirect_memory
